#!/usr/bin/perl -w

# $Id: gpt-wizard,v 1.9 2002-08-09 19:58:07-07 kst Exp $
# $Source: /home/kst/CVS_smov/tools/gpt-wizard/gpt-wizard,v $

use strict;

use File::Basename ();
use Getopt::Long ();
use POSIX ();

sub Get_Config($);
sub Substitute($);
sub Elapsed_Time_Image($);
sub Time_Image();
sub Make_Temp_File($);
sub Get_Result($$);
sub Execute(@);
sub Usage(@);

my $Prog_Name = File::Basename::basename $0;
my $temp_file_serial_number = 0;

Usage if scalar @ARGV != 1;
my %Config = Get_Config $ARGV[0];

my @As_Globus = qw(-time -verbose -check -user=globus);
my @As_Root   = qw(-time -verbose -check);

my $CLK_TCK;
eval { $CLK_TCK = &POSIX::CLK_TCK };
if ($@) {
    $CLK_TCK = 100;
    # warn ">>> CLK_TCK not available, assuming 100\n";
}

if ($< != 0) {
    die ">>> $Prog_Name must be run as root\n";
}

$| = 1;

my $start_dir = `pwd`;
die "pwd failed\n" if $? != 0;
chomp $start_dir;

if ($Config{OS} ne 'linux') {
    die "Unsupported OS: $Config{OS}\n";
}

my @globus_pw = getpwnam 'globus';
die ">>> No user globus\n" unless $#globus_pw >= 2;
my @globus_gr = getgrnam 'globus';
die ">>> No group globus\n" unless $#globus_gr >= 2;
my $globus_uid = $globus_pw[2];
my $globus_gid = $globus_gr[2];
die ">>> globus_uid = $globus_uid\n" unless $globus_uid > 0;
die ">>> globus_gid = $globus_gid\n" unless $globus_gid > 0;

my $bundle_dir        = '/home/kst/src/NMI/linux/gpt-bundles';
my $nmi_client_bundle = "$bundle_dir/nmi-client-NMI-1.0.0-i686.gpt.tar.gz";
my $nmi_server_bundle = "$bundle_dir/nmi-server-NMI-1.0.0-i686.gpt.tar.gz";

#
# Install GPT
#
my $tmp_dir = "/tmp/gpt-build-$$";
print Time_Image, "\n";
mkdir $tmp_dir or die "$tmp_dir: $!\n";
chown $globus_uid, $globus_gid, $tmp_dir or die "$tmp_dir: $!\n";
chdir $tmp_dir or die "$tmp_dir: $!\n";
Execute @As_Globus, 'tar', 'zxf', $Config{GPT_TARBALL};
my $gpt_src_dir = File::Basename::basename $Config{GPT_TARBALL};
$gpt_src_dir =~ s/\.tar\.gz$//;
$gpt_src_dir =~ s/-src$//;
chdir $gpt_src_dir or die "${gpt_src_dir}: $!\n";
$ENV{GPT_LOCATION} = $Config{GPT_LOCATION};
mkdir $ENV{GPT_LOCATION} or die "$ENV{GPT_LOCATION}: $!\n";
chown $globus_uid, $globus_gid, $ENV{GPT_LOCATION}
    or die "$ENV{GPT_LOCATION}: $!\n";
Execute @As_Globus, './build_gpt';

#
# <http://www.nsf-middleware.org/documentation/GPT/index.htm>
# says to delete these two files.  They seem to be installed
# if you use the RPM, but not if you use the source tarball.
#
my @files_to_delete = ();
foreach my $file ('/etc/profile.d/gpt.sh', '/etc/profile.d/gpt.csh') {
    push @files_to_delete, $file if -e $file;
}
if (@files_to_delete) {
    print "% unlink @files_to_delete\n";
    unlink @files_to_delete or warn ">>> $!\n";
}

print "% chdir $start_dir\n";
chdir $start_dir or die "${start_dir}: $!\n";
Execute '-verbose', 'rm', '-rf', $tmp_dir;

#
# Confirm GPT installation
#
Execute @As_Globus, "$ENV{GPT_LOCATION}/sbin/gpt-install", '-version';

$ENV{GLOBUS_LOCATION} = $Config{GLOBUS_LOCATIN};
mkdir $ENV{GLOBUS_LOCATION} or die "$ENV{GLOBUS_LOCATION}: $!\n";
chown $globus_uid, $globus_gid, $ENV{GLOBUS_LOCATION}
    or die "$ENV{GLOBUS_LOCATION}: $!\n";

foreach my $bundle ( $Config{NMI_CLIENT_BUNDLE},
                     $Config{NMI_SERVER_BUNDLE})
{
    Execute @As_Globus, "$ENV{GPT_LOCATION}/sbin/gpt-install", $bundle;
}

my $input_file;

Execute @As_Globus, "-input=0\n0\nn\n\n",
        "$ENV{GPT_LOCATION}/sbin/gpt-postinstall";

Execute @As_Root, "-input=y\nq\n",
        "$ENV{GLOBUS_LOCATION}/setup/globus/setup-gsi";

Execute qw(-verbose -check),
        'cp', $Config{GRID_MAPFILE}, '/etc/grid-security/grid-mapfile';
Execute qw(-verbose -check),
        'cp', $Config{HOST_CERT},    '/etc/grid-security/hostcert.pem';
Execute qw(-verbose -check),
        'cp', $Config{HOST_KEY},     '/etc/grid-security/hostkey.pem';

chmod 0444, '/etc/grid-security/grid-mapfile';
chmod 0444, '/etc/grid-security/hostcert.pem';
chmod 0400, '/etc/grid-security/hostkey.pem';

chdir '/etc/grid-security' or die "/etc/grid-security: $!\n";
rename 'certificates', 'certificates.orig';
Execute qw(-verbose -check),
        'tar', 'zxf', $Config{CA_CERT_TARBALL};

opendir DIR, $Config{XINETD_DIR} or die "$Config{XINETD_DIR}: $!\n";
my @files = grep { $_ ne '.' and $_ ne '..' } sort readdir DIR;
closedir DIR;

foreach my $file (@files) {
    print "Copying $file to /etc/xinetd.d\n";
    open IN, "< $Config{XINETD_DIR}/$file"
        or die "$Config{XINETD_DIR}/${file}: $!\n";
    open OUT, "> /etc/xinetd.d/$file"
        or die "/etc/xinetd.d/${file}: $!\n";
    while (<IN>) {
        s/\@(\w+)\@/Substitute $1/eg;
        print OUT;
    }
    close IN;
    close OUT;
}

Execute qw(-verbose -check), qw(/etc/init.d/xinetd reload);

Execute qw(-verbose -check -user=globus),
        "$ENV{GLOBUS_LOCATION}/sbin/SXXgris", 'start';

########################################################################

#
# Read and parse the config file (name given on the command line) and
# return the results as a hash (to be assigned to the global %Config).
#
sub Get_Config($) {
    my %Result = ();
    my($config_file) = @_;
    print "Getting configuration from $config_file\n";

    my %options = ( OS                => 0,
                    GPT_BUNDLE        => 0,
                    NMI_CLIENT_BUNDLE => 0,
                    NMI_SERVER        => 0,
                    GPT_LOCATION      => 0,
                    GLOBUS_LOCATION   => 0,
                    GRID_MAPFILE      => 0,
                    HOST_CERT         => 0,
                    HOST_KEY          => 0,
                    CA_CERT_TARBALL   => 0,
                    XINETD_DIR        => 0 );

    open CONFIG, $config_file or die "$config_file: $!\n";
    while (<CONFIG>) {
        s/#.*$//;
        s/^\s+//;
        s/\s+$//;
        next if /^$/;
        if (/^(\w+)\s*(.*)$/) {
            my ($name, $value) = ($1, $2);
            if (defined $options{$name}) {
                $Result{$1} = $2;
                $options{$1} ++;
            }
            else {
                die "Unrecognized option in $config_file at line $.\n";
            }
        }
        else {
            die "Syntax error in $config_file at line $.\n";
        }
    }
    close CONFIG;

    my @missing = ();
    my @extra = ();
    foreach my $option (sort keys %options) {
        if ($options{$option} == 0) {
            push @missing, $option;
        }
        elsif ($options{$option} > 1) {
            push @extra, $option;
        }
    }

    if (@missing or @extra) {
        my @errors = ( ">>> Bad config file $config_file\n" );
        foreach (@missing) {
            push @errors, "    Missing option $_\n";
        }
        foreach (@extra) {
            push @errors, "    Multiply specified option $_\n";
        }
        die @errors;
    }

    #
    # OS name is case-insensitive; map to lower case.
    #
    $Result{OS} = lc $Result{OS};

#   if ($Debugging) {
#       print "Result:\n";
#       foreach my $key (sort keys %Result) {
#           printf "    %-20s => \"%s\"\n", $key, $Result{$key};
#       }
#   }
    return %Result;
} # Get_Config

# ----------------------------------------------------------------------

sub Substitute($) {
    my($key) = @_;
    if (defined $Config{$key}) {
        return $Config{$key};
    }
    else {
        die "Unrecognized substitution key: $key\n";
    }
} # Substitute

# ----------------------------------------------------------------------

sub Elapsed_Time_Image($) {
    my($seconds) = @_;

    my $h = int($seconds / 3600);
    $seconds -= 3600 * $h;      # Can't use "%"; it truncates.

    my $m = int($seconds / 60);
    $seconds -= 60 * $m;

    if ($h > 0) {
        return sprintf '%d:%02d:%05.2f', $h, $m, $seconds;
    }
    else {
        return sprintf '%d:%05.2f', $m, $seconds;
    }
} # Elapsed_Time_Image

# ----------------------------------------------------------------------

sub Time_Image() {
    #
    # Kludge: There's no good way (as far as I know) to get the name
    # of the time zone, so we invoke "date +%Z" and grab the output.
    # Barring a lot of extra work, this works only for the current time,
    # which is why this version of Time_Image has no time parameter.
    #
    # There's a tiny chance of a glitch around DST transitions, since the
    # calls to the time function and the date program aren't simultaneous.
    # Oh well.
    #
    my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst)
        = localtime time;
    my $tz = `date +%Z`;
    chomp $tz;
    my $result;

    return sprintf "%s %04d-%02d-%02d %02d:%02d:%02d %s",
                   (qw(Sun Mon Tue Wed Thu Fri Sat))[$wday],
                   1900+$year, 1+$mon, $mday, $hour, $min, $sec, $tz;
} # Time_Image

# ----------------------------------------------------------------------

sub Make_Temp_File($) {
    my($content) = @_;
    my $filename = sprintf "/tmp/tmp-%d-%d-%02d",
                           time, $$, $temp_file_serial_number ++;
    open TMP, ">$filename" or die "${filename}: $!\n";
    print TMP $content;
    close TMP;
    return $filename;
} # Make_Temp_File

# ----------------------------------------------------------------------

sub Get_Result($$) {
    my ($status, $check) = @_;
    my $Result = {};
    my $hi_8 = ($status & 0xff00) >> 8;
    my $lo_8 =  $status & 0x00ff;

    my $WSTOPFLG = 0x007f;
    my $WCOREFLG = 0x0080;

    if ($lo_8 == $WSTOPFLG) {
        $Result->{signal} = $hi_8;
        $Result->{stopped} = 1;
        $Result->{desc} = "stopped, signal $hi_8";
        die $Result->{desc} if $check;
    }
    elsif ($lo_8 == 0) {
        $Result->{status} = $hi_8;
        $Result->{desc} = "exit $hi_8";
        die $Result->{desc} if $check and $Result->{status} != 0;
    }
    elsif ($hi_8 == 0) {
        my $signal = $lo_8;
        if ($signal & $WCOREFLG) {
            $signal &= ~$WCOREFLG;
            $Result->{core} = 1;
        }
        $Result->{signal} = $signal;
        $Result->{desc} = "signal $signal";
        if ($Result->{core}) {
            $Result->{desc} .= " (core dumped)";
        }
        die $Result->{desc} if $check;
    }
    return $Result;
} # Get_Result

# ----------------------------------------------------------------------

sub Execute(@) {
    my @args = @_;
    my $Verbose = 0;
    my $Time = 0;
    my $Background = 0;
    my $Check = 0;
    my $User = undef;
    my $Stdin = undef;
    my $Stdin_Is_Temporary = undef;
    my @before;
    my @after;
    my $system_result;

    #
    # Process arguments
    #
    while ( scalar @args > 0 && $args[0] =~ /^-/) {
        my $arg = shift @args;
        if ($arg eq '-verbose') {
            $Verbose = 1;
        }
        elsif ($arg eq '-time') {
            $Time = 1;
        }
        elsif ($arg eq '-background') {
            $Background = 1;
        }
        elsif ($arg eq '-check') {
            $Check = 1;
        }
        elsif ($arg =~ /^-user=(.+)$/) {
            $User = $1;
        }
        elsif ($arg =~ /^-stdin=(.+)$/) {
            $Stdin = $1;
        }
        elsif ($arg =~ /^-input=(.+)$/s) {
            #
            # Need to use "/s" modifier so "." matches "\n"
            #
            $Stdin = Make_Temp_File $1;
            $Stdin_Is_Temporary = 1;
        }
        else {
            die "Execute: Unrecognized argument: $arg\n";
        }
    }

    if (defined $User) {
        my @pw = getpwnam $User;
        die "${User}: No such user\n" if not @pw;
        my $uid = $pw[2];
        my $pid;
        if (($pid = fork) == 0) {
            #
            # This is the child process.
            # Set the real and effective user id, and contine to handle
            # the call.
            #
            $< = $uid;
            $> = $uid;
        }
        else {
            #
            # This is the parent process.
            # Wait for the child to finish.  
            #
            waitpid $pid, 0;
            return Get_Result $?, $Check;
        }
    }

    if ($Verbose) {
        my $command_string = '% ';
        $command_string .= "[$User] " if defined $User;
        $command_string .= 'time ' if $Time;
        $command_string .= "@args";
        $command_string .= " < $Stdin" if defined $Stdin;
        $command_string .= " &" if $Background;
        print "$command_string\n";
    }

    if ($Background) {
        my $pid = fork;
        if ($pid != 0) {
            #
            # I'm the parent
            #
            my $Result = {};
            $Result->{pid} = $pid;
            return $Result;
        }
    }

    if (defined $Stdin) {
        open SAVE_STDIN, "<&STDIN";
        print SAVE_STDIN if 0;          # keep "perl -w" happy
        close STDIN;
        open STDIN, "<$Stdin" or die "${Stdin}: $!\n";
    }
    if ($Time) {
        @before = POSIX::times;
    }
    $system_result = system @args;
    if ($Time) {
        @after = POSIX::times();
    }
    if (defined $Stdin) {
        open STDIN, "<&SAVE_STDIN";
        unlink $Stdin if $Stdin_Is_Temporary;
    }

    #
    # Emulate csh "time" command
    #
    if ($Time) {
        my $elapsed_ticks    =  $after[0] - $before[0];
        my $user_CPU_ticks   = ($after[1] - $before[1]) +   # this process
                               ($after[3] - $before[3]);    # child processes
        my $system_CPU_ticks = ($after[2] - $before[2]) +   # this process
                               ($after[4] - $before[4]);    # child processes

        # print "\$elapsed_ticks    = $elapsed_ticks\n";
        # print "\$user_CPU_ticks   = $user_CPU_ticks\n";
        # print "\$system_CPU_ticks = $system_CPU_ticks\n";
        my $percent_usage;
        if ($elapsed_ticks == 0) {
            $percent_usage = 0.0;
        }
        else {
            $percent_usage = ($user_CPU_ticks + $system_CPU_ticks)
                             / $elapsed_ticks * 100;
        }

        my $elapsed_seconds    = $elapsed_ticks    / $CLK_TCK;
        my $user_CPU_seconds   = $user_CPU_ticks   / $CLK_TCK;
        my $system_CPU_seconds = $system_CPU_ticks / $CLK_TCK;

        printf "%.2fu %.2fs %s %.1f%%\n",
               $user_CPU_seconds,
               $system_CPU_seconds,
               Elapsed_Time_Image $elapsed_seconds,
               $percent_usage;
    }
    if ($Background or defined $User) {
        #
        # This is the child process; bail out and let the parent handle
        # the consequences.
        #
        exit(($system_result & 0xff00) >> 8);
    }

    return Get_Result($system_result, $Check);
} # Execute

# ----------------------------------------------------------------------

sub Usage(@) {
    print @_ if @_;
    print <<"EOF";
Usage: $Prog_Name config_file
EOF
    exit 1;
} # Usage
