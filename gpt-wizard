#!/usr/bin/perl -w

# $Id: gpt-wizard,v 1.171 2003-03-05 14:55:04-08 kst Exp $
# $Source: /home/kst/CVS_smov/tools/gpt-wizard/gpt-wizard,v $

########################################################################
# @Copyright@
# 
# Copyright (c) 2003 The Regents of the University of California. All
# rights reserved.
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
# 
# 1. Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
# 
# 2. Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
# 
# 3. All advertising materials mentioning features or use of this
# software must display the following acknowledgement: This product
# includes software developed by the Grid and Cluster Computing Group
# at the San Diego Supercomputer Center and its contributors.
# 
# 4. Neither the name of the Center nor the names of its contributors
# may be used to endorse or promote products derived from this software
# without specific prior written permission.
# 
# THIS SOFTWARE IS PROVIDED BY THE REGENTS AND CONTRIBUTORS ``AS IS''
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
# THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
# PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE REGENTS OR CONTRIBUTORS
# BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
# BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
# WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
# OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
# IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
# 
# @Copyright@
########################################################################

########################################################################
# Developed by Keith Thompson <kst@sdsc.edu>
########################################################################

#
# TO DO:
#
#     Start services other than GRIS (make it configurable).
#
#     May need to update lines in inetd.conf or files in xinetd.d
#     for services other than gsigatekeeper and gsiftp.
#
#     Automatically set up to report to one or more GIIS servers.
#     Configure this.
#
#     Be smarter about tarballs.  Handle .tar, .tar.gz, .tgz,
#     .tar.bz2.  Auto-detect what tar can handle (check output
#     of "tar --help") and fall back to gzip/bzip2 and pipes.
#     (Particularly useful for non-Linux systems.)
#
#     Smarter handling of flavors for source installations,
#     particularly for vendorcc; see build-gt2.
#
#     Detect 64-bit machines other than ia64.
#     If it's configurable, configure it.
#
#     Optionally use "setup-gsi -nonroot".  This should install the
#     Globus security stuff in $GLOBUS_LOCATION/etc.  This is useful
#     for a client-only installation.
#
#     More generally, allow gpt-wizard to be executed, with some
#     restrictions, by a non-root account (preferably "globus").
#     Anything that needs to be done by root would be added to
#     @manual_steps.
#
#     Possibly make "setup-gsi" optional, configure /etc/grid-security
#     by brute force.  Find out more about the implications of doing this.
#
#     Optionally allow use of an existing /etc/grid-security directory.
#     If this option is not selected, warn (or die?) if one already
#     exists.
#
#     Allow GPT to be installed from an RPM or binary package.
#
#     Deal with globus-host-ssl.conf.42864e48
#               globus-user-ssl.conf.42864e48
#               grid-security.conf.42864e48
#
#     If GLOBUS_HOSTNAME is specified, set the $GLOBUS_HOSTNAME
#     environment variable in inetd.conf or xinetd config files.
#     (Is this necessary?)
#
#     Allow references to existing environment variables in ENV options.
#     For example:
#         ENV   LD_LIBRARY_PATH=/foo/bar:$LD_LIBRARY_PATH
#     On second thought, this might not be such a good idea; it allows
#     too much dependence on the existing environment.
#
#     Optionally generate a Bourne shell script to do the installation
#     rather than actually doing the installation.
#
#     Replace GLOBUS_VAR_DIRECTORY with a more general GLOBUS_LOCAL_DIRECTORY.
#     Everything that needs to be localized would be replaced by
#     symlinks into GLOBUS_LOCAL_DIRECTORY; for example:
#         GLOBUS_LOCATION/var --> GLOBUS_LOCAL_DIRECTORY/var
#         GLOBUS_LOCATION/etc/globus-job-manager.conf
#             --> GLOBUS_LOCAL_DIRECTORY/etc/globus-job-manager.conf 
#     See Globus Bugzilla #660,
#     <http://bugzilla.globus.org/bugzilla/show_bug.cgi?id=660>.
#     
#     Specify CVS files somehow -- perhaps via a fake "cvs://" URL (ick)
#     and/or add an optional config var or two to specify how to get
#     files from CVS.
#

require 5.004;

use strict;

use File::Basename ();
use Getopt::Long ();
use POSIX ();

sub Strip_Revision_String($);
sub Get_Config($);
sub Get_System_Information();
sub URL_Protocol($);
sub Check_URLs(@);
sub Download_Files(@);
sub Elapsed_Time_Image($);
sub Time_Image($);
sub Make_Temp_File($);
sub Get_Result($$);
sub Get_Update_Flavors($);
sub Execute(@);
sub Check_Services();
sub Die_Handler($);
sub Timestamp_Of($);
sub Backup($);
sub Is_Empty_Dir($);
sub Fix_Setup_Script($);
sub Inet_Config_Text($$);
sub Image($);
sub Show_Version();
sub Release_String();
sub Debug(@);
sub Usage(@);

#
# Make warnings fatal.
#
$SIG{__WARN__} = sub { die @_ };

$| = 1;

#
# The following Release_Info table must be updated manually on each
# new release.  It's intended to map release numbers to RCS revisions,
# which should should correspond to RCS symbolic names.  (Automatating
# this is theoretically possible, but probably more effort than it's
# worth.)
#
my $Release_Info = [ { release => '0.1',   revision => '1.144' },
                     { release => '0.1.1', revision => '1.146' } ];

my @Saved_ARGV        = @ARGV;
my $Prog_Name         = File::Basename::basename $0;
my $Revision          = Strip_Revision_String '$Revision: 1.171 $';
my $Help              = undef;
my $Show_Version      = undef;
my $Validate_Only     = undef;
my $Debugging         = undef;
my $Cleanup           = 1;
my $Postinstall       = 1;
my $Startup           = undef;
my $GPT_Verbose       = undef;
my $GPT_Log           = undef;
my $GPT_Install_Only  = undef;
my $GPT_Ignore_Errors = undef;
my $AIX_Kludge        = undef;
my @Opts = ( 'help!'              => \$Help,
             'version!'           => \$Show_Version,
             'validate-only!'     => \$Validate_Only,
             'debugging!'         => \$Debugging,
             'cleanup!'           => \$Cleanup,
             'postinstall!'       => \$Postinstall,
             'startup!'           => \$Startup,
             'gpt-verbose!'       => \$GPT_Verbose,
           # 'gpt-log!'           => \$GPT_Log,
             'gpt-install-only!'  => \$GPT_Install_Only,
             'gpt-ignore-errors!' => \$GPT_Ignore_Errors,
             'aix-kludge!'        => \$AIX_Kludge );
Getopt::Long::GetOptions @Opts or Usage;
Usage if $Help;
Show_Version if $Show_Version;

die "-gpt-log option is disabled\n" if $GPT_Log;

if ($GPT_Log and $Cleanup) {
    print "-gpt-log implies -nocleanup, setting option\n";
    $Cleanup = undef;
}

if ($Startup and not $Postinstall) {
    Usage "Option conflict: \"-startup\" and \"-nopostinstall\"\n",
          "    (Can't do startup without doing gpt-postinstall)\n";
}

$SIG{__DIE__} = \&Die_Handler;

#
# A hash mapping URLs (ftp or http) to cached local files.
#
my $Download_Initialized = undef;
my %Local_File = ();
my $download_index = 0;
my $wget = undef;

my $temp_file_serial_number = 0;
my @manual_steps = ();

#
# Attributes for bundles and updates:
#
my %Bundle_Kind   = ();
my %Bundle_Option = ();
my %Bundle_Flavor = ();
my %Is_Update     = ();

Usage if scalar @ARGV != 1;
my $Config_File = $ARGV[0];
my %Config = Get_Config $Config_File;

my %Sys = Get_System_Information;

my $Initial_Dir = `pwd`;
chomp $Initial_Dir;

#
# If any bundles were installed using RPM, we'll need to run
# gpt-postinstall as root.
#
my $Using_RPM = 0;

if ($Sys{LC_OSNAME} eq 'linux') {
    # ok
}
elsif ($Sys{LC_OSNAME} eq 'sunos' or
       $Sys{LC_OSNAME} eq 'aix') {
    if ($Startup) {
        die "The \"-startup\" option is not currently supported " .
            "on $Sys{OSNAME}\n";
    }
}
else {
    die "Unsupported OS \"$Sys{OSNAME}\"\n";
}

Check_Services;

my @As_Root      =  qw(-time=5 -verbose -check);
my @As_Installer = (qw(-time=5 -verbose -check),
                    "-user=$Config{INSTALLER_USER}.$Config{INSTALLER_GROUP}");
my @As_Root_GPT      = @As_Root;
my @As_Installer_GPT = @As_Installer;
if ($GPT_Ignore_Errors) {
    @As_Root_GPT      = grep { $_ ne '-check' } @As_Root;
    @As_Installer_GPT = grep { $_ ne '-check' } @As_Installer;
}

my $CLK_TCK = POSIX::sysconf(&POSIX::_SC_CLK_TCK);

if ($< != 0) {
    die ">>> $Prog_Name must be run as root\n";
}

if (exists $Config{urls}) {
    die "No DOWNLOAD_DIR specified\n" if not defined $Config{DOWNLOAD_DIR};
    Check_URLs @{$Config{urls}};
}

exit 0 if $Validate_Only;

########################################################################
# Create a temporary working directory and set some variables.
########################################################################

my $build_info = Time_Image(time) . "\n";
$build_info .= "$Prog_Name revision $Revision processing $Config_File";
if (defined $Config{CONFIG_FILE_REVISION}) {
    $build_info .= " $Config{CONFIG_FILE_REVISION}\n";
}
else {
    $build_info .= ", no CONFIG_FILE_REVISION specified\n";
}
print $build_info;

my @installer_pw = getpwnam $Config{INSTALLER_USER};
die ">>> No user $Config{INSTALLER_USER}\n" unless $#installer_pw >= 2;
my @installer_gr = getgrnam $Config{INSTALLER_GROUP};
die ">>> No group $Config{INSTALLER_GROUP}\n" unless $#installer_gr >= 2;
my $installer_uid = $installer_pw[2];
my $installer_gid = $installer_gr[2];
die ">>> installer_uid = $installer_uid\n" unless $installer_uid > 0;
# die ">>> installer_gid = $installer_gid\n" unless $installer_gid > 0;

print "% setting PATH=$Config{PATH}\n";
$ENV{PATH} = $Config{PATH};

foreach my $env (@{$Config{ENV}}) {
    my($name, $value);
    if ($env =~ /^([^=]+)=(.*)$/) {
        ($name, $value) = ($1, $2);
    }
    elsif ($env =~ /^[^=]*$/) {
        die "Invalid ENV option: \"$env\"\n";
    }
    else {
        $name = $env;
        $value = '';
    }
    print "% setting $name=\"$value\"\n";
    $ENV{$name} = $value;
}

print "% umask $Config{UMASK}\n";
umask oct $Config{UMASK} or die "umask $Config{UMASK}: $!\n";

my $working_dir = "$Config{TEMP_DIR}/gpt-wizard-" . time . "-$$";
print "% mkdir $working_dir\n";
mkdir $working_dir, 0700 or die "$working_dir: $!\n";
chown $installer_uid, $installer_gid, $working_dir
    or die "$working_dir: $!\n";
print "% cd $working_dir\n";
chdir $working_dir or die "$working_dir: $!\n";
if (not $Cleanup) {
    push @manual_steps,
         "The working directory\n" .
         "    $working_dir\n" .
         "is likely to be taking up a lot of disk space.  Clean it up\n" .
         "when you're done with it.\n";
}

########################################################################
# Download any files specified by ftp:// or http:// URLs
# For security reasons, certificates and keys may not be downloaded.
########################################################################
my @file_names = ();

foreach my $var (qw(GPT_TARBALL GRID_MAPFILE CA_CERT_TARBALL)) {
    push @file_names, $Config{$var} if defined $Config{$var};
}

for (my $i = 0; $i <= $#{$Config{BUNDLE}}; $i ++) {
    push @file_names, $Config{BUNDLE}->[$i];
}
for (my $i = 0; $i <= $#{$Config{UPDATE}}; $i ++) {
    push @file_names, $Config{UPDATE}->[$i];
}

Download_Files @file_names;

########################################################################
# Set the $GLOBUS_LOCATION environment variable if specified.
# This is necessary if the system hostname doesn't match the
# externally visible network name (e.g., on a typical Rocks cluster).
########################################################################
if (defined $Config{GLOBUS_LOCATION}) {
    $ENV{GLOBUS_LOCATION} = $Config{GLOBUS_LOCATION};
}

########################################################################
# Install GPT
########################################################################

########################################
# Unpack the source tarball.  See what the tar command created.
# If there's exactly one new thing in the current directory, assume it's
# the extracted source directory.
########################################

if (not $AIX_Kludge) {
    opendir DIR, '.' or die ".: $!\n";
    my @files_before = readdir DIR;
    closedir DIR;
    my %already_existed = map { $_ => 1 } @files_before;

    # Execute @As_Installer, 'tar', 'zxf', $Local_File{$Config{GPT_TARBALL}};
    Execute @As_Installer, 'sh', '-c',
            "gzip -d < $Local_File{$Config{GPT_TARBALL}} | tar xf -";

    opendir DIR, '.' or die ".: $!\n";
    my @files_after = readdir DIR;
    closedir DIR;

    my @new_files = ();
    foreach my $file (@files_after) {
        push @new_files, $file if not $already_existed{$file};
    }

    if (scalar @new_files == 0) {
        die "Can't determine GPT source directory, " .
            "tar didn't create anything\n";
    }
    elsif (scalar @new_files == 1) {
        my $src_dir = $new_files[0];
        print "% cd $working_dir/$src_dir\n";
        chdir "$working_dir/$src_dir" or die "$working_dir/$src_dir: $!\n";
    }
    else {
        die "Can't determine GPT source directory, tar created (@new_files)\n";
    }
}

########################################
# Create the $GPT_LOCATION directory.
########################################
print "% Setting GPT_LOCATION=$Config{GPT_LOCATION}\n";
$ENV{GPT_LOCATION} = $Config{GPT_LOCATION};

if ($AIX_Kludge) {
    die "No directory $Config{GPT_LOCATION}\n"
        if not -d $Config{GPT_LOCATION};
    die "No executable $Config{GPT_LOCATION}/sbin/gpt-install\n"
        if not -x "$Config{GPT_LOCATION}/sbin/gpt-install";
}
else {
    if (not -d $Config{GPT_LOCATION}) {
        print "% mkdir $Config{GPT_LOCATION}\n";
        mkdir $Config{GPT_LOCATION}, 0777 or die "$Config{GPT_LOCATION}: $!\n";
        chown $installer_uid, $installer_gid, $Config{GPT_LOCATION}
            or die "$Config{GPT_LOCATION}: $!\n";
    }
}

########################################
# Run build-gpt to install GPT
########################################
if (not $AIX_Kludge) {
    Execute @As_Installer, './build_gpt';

    print "% cd $working_dir\n";
    chdir $working_dir or die "$working_dir: $!\n";
}

########################################
# <http://www.nsf-middleware.org/documentation/GPT/index.htm>
# says to delete these two files.  They seem to be installed
# if you use the RPM, but not if you use the source tarball.
# So in reality, these files probably won't exist in the first place;
# ignore any errors from unlink().
########################################
unlink '/etc/profile.d/gpt.sh',
       '/etc/profile.d/gpt.csh';

########################################################################
# Confirm GPT installation
########################################################################
#
# In older GPT releases, the gpt-build and gpt-install commands
# are called globus-build and globus-install.
#
my $gpt_install_command = undef;
my $gpt_build_command   = undef;
my $sbin_dir = "$Config{GPT_LOCATION}/sbin";
my $using_old_gpt;

if (-x "$sbin_dir/gpt-install" and -x "$sbin_dir/gpt-build") {
    $gpt_install_command = "$sbin_dir/gpt-install";
    $gpt_build_command   = "$sbin_dir/gpt-build";
    $using_old_gpt = 0;
}
elsif (-x "$sbin_dir/globus-install" and -x "$sbin_dir/globus-build") {
    $gpt_install_command = "$sbin_dir/globus-install";
    $gpt_build_command   = "$sbin_dir/globus-build";
    $using_old_gpt = 1;
}
else {
    die "Can't find install and build commands in $sbin_dir\n";
}

#
# Verify GPT installation.
# For recent releases of GPT, verify that "gpt-install -version" succeeds.
# In older releases, globus-install doesn't support the "-version"
# option; we've already checked that the command exists.
#
if (not $using_old_gpt) {
    Execute @As_Installer, $gpt_install_command, '-version';
}

########################################################################
# Install bundles (presumably Globus et al)
########################################################################

########################################
# Create the $GLOBUS_LOCATION directory (unless $GLOBUS_LOCATION and
# $GPT_LOCATION are the same; in that case, it already exists.)
########################################
print "% Setting GLOBUS_LOCATION=$Config{GLOBUS_LOCATION}\n";
$ENV{GLOBUS_LOCATION} = $Config{GLOBUS_LOCATION};
if ($Config{GLOBUS_LOCATION} ne $Config{GPT_LOCATION} and
    not -e $Config{GLOBUS_LOCATION})
{
    print "% mkdir $Config{GLOBUS_LOCATION}\n";
    mkdir $Config{GLOBUS_LOCATION}, 0777
        or die "$Config{GLOBUS_LOCATION}: $!\n";
    chown $installer_uid, $installer_gid, $Config{GLOBUS_LOCATION}
        or die "$Config{GLOBUS_LOCATION}: $!\n";
}

########################################
# Create a $GLOBUS_LOCATION/.gpt-wizard directory, to contain information
# about the build, including a copy of the config file.
########################################
my $info_dir = "$Config{GLOBUS_LOCATION}/.gpt-wizard";
my $info_file = "$info_dir/info";

print "% mkdir $info_dir\n";
mkdir $info_dir, 0777 or die "$info_dir: $!\n";
chown $installer_uid, $installer_gid, $info_dir or die "$info_dir: $!\n";

Execute @As_Installer, 'cp', '-p', "$Initial_Dir/$Config_File", "$info_dir/.";

open INFO, ">$info_file" or die "$info_file: $!\n";
print INFO $build_info;
print INFO "Command-line arguments:\n";
foreach my $arg (@Saved_ARGV) {
    print INFO "    $arg\n";
}
close INFO;

########################################
# Run gpt-install or gpt-build to install the bundles.
########################################
foreach my $bundle ( @{$Config{BUNDLE}}, @{$Config{UPDATE}} ) {
    my $bundle_basename = File::Basename::basename $bundle;

    #
    # If the file name was specified in the config file with a
    # "kind=binary" attribute, treat it as a binary bundle (use
    # gpt-install).
    #
    # If it was specified with a "kind=source" suffix, treat it as a
    # source bundle (use gpt-build).
    #
    # If it was specified with a "kind=rpm" suffix, treat it as an RPM
    # bundle (use gpt-install, and run it as root).
    #
    # If no "kind" attribute was specified:
    #      If it's an update, assume it's source.
    #      If the file name contains the word "rpm", assume it's an
    #      RPM bundle.
    #      Otherwise, if the file name contains the word "src" assume
    #      it's a source bundle.
    #      Otherwise, assume it's a binary bundle.
    #
    # This is arguably too complicated; consider making the "kind"
    # attribute mandatory, at least for non-updates.
    #
    my $bundle_kind = $Bundle_Kind{$bundle};
    if (not defined $bundle_kind) {
        if ($Is_Update{$bundle}) {
            $bundle_kind = 'source';
        }
        elsif ($bundle_basename =~ /(\b|_)src(\b|_)/) {
            $bundle_kind = 'source';
        }
        elsif ($bundle_basename =~ /(\b|_)rpm(\b|_)/) {
            $bundle_kind = 'rpm';
        }
        else {
            $bundle_kind = 'binary';
        }
    }

    if ($bundle_kind eq 'source') {
        #
        # Using gpt-build to build and install a source bundle.
        #
        my @options = ();
        if ($GPT_Verbose) {
            push @options, '-verbose';
        }
        if ($GPT_Log) {
            my $logfile = File::Basename::basename $bundle . ".log";
            push @options, "-log=$logfile";
        }
        if ($GPT_Install_Only) {
            push @options, '-install-only';
        }
        if ($Is_Update{$bundle}) {
            push @options, '-update';
        }
        if (defined $Bundle_Option{$bundle}) {
            push @options, split(',', $Bundle_Option{$bundle});
        }

        my @flavors = ();
        if (defined $Bundle_Flavor{$bundle}) {
            @flavors = split /,/, $Bundle_Flavor{$bundle};
        }
        elsif ($Is_Update{$bundle}) {
            @flavors = Get_Update_Flavors $bundle;
        }
        else {
            push @options, '-std-flavors';
        }
        Execute @As_Installer_GPT,
                $gpt_build_command, @options, $Local_File{$bundle}, @flavors;
    }
    elsif ($bundle_kind eq 'binary' or $bundle_kind eq 'rpm') {
        #
        # Using gpt-install to install a binary or RPM bundle.
        #
        my @options = ();
        if ($GPT_Verbose and not $using_old_gpt) {
            push @options, '-verbose';
        }
        #
        # $GPT_Log and $GPT_Install_Only are inapplicable for gpt-install.
        #
        if ($bundle_kind eq 'binary') {
            Execute @As_Installer_GPT,
                    $gpt_install_command, @options, $Local_File{$bundle};
        }
        else {
            $Using_RPM = 1;
            Execute @As_Root_GPT,
                    $gpt_install_command, @options, $Local_File{$bundle};
        }
    }
    else {
        #
        # The missing "\n" in the error message is deliberate.  It causes
        # Perl to print more information about the location of the error.
        #
        die "Internal error, \$bundle_kind = \"$bundle_kind\"";
    }
}

########################################
# If GLOBUS_VAR_DIRECTORY is defined in the config file, make
# $GLOBUS_LOCATION/var a symlink to it (after copying it if it exists,
# or creating it if it doesn't).
# (The $GLOBUS_LOCATION/var directory must be on a local filesystem,
# so root can write to logs.)
########################################
if (defined $Config{GLOBUS_VAR_DIRECTORY}) {
    if (-d "$ENV{GLOBUS_LOCATION}/var") {
        Execute @As_Root, 'cp', '-pr', "$ENV{GLOBUS_LOCATION}/var",
                                       $Config{GLOBUS_VAR_DIRECTORY};
        Execute @As_Root, 'rm', '-rf', "$ENV{GLOBUS_LOCATION}/var";
    }
    else {
        print "% mkdir $Config{GLOBUS_VAR_DIRECTORY}\n";
        mkdir $Config{GLOBUS_VAR_DIRECTORY}, 0700
            or die "$Config{GLOBUS_VAR_DIRECTORY}: $!\n";
        chown $installer_uid, $installer_gid, $Config{GLOBUS_VAR_DIRECTORY}
            or die "$Config{GLOBUS_VAR_DIRECTORY}: $!\n";
    }
    symlink $Config{GLOBUS_VAR_DIRECTORY}, "$ENV{GLOBUS_LOCATION}/var"
        or die "$ENV{GLOBUS_LOCATION}/var: $!\n";
}


########################################################################
# Post-installation
########################################################################

########################################
# Edit certain setup files so they don't prompt for user input.  Ugly,
# but it beats guessing what they're going to want (and in what order
# they'll be run).
########################################

Fix_Setup_Script
    "$ENV{GLOBUS_LOCATION}/setup/globus/grid-security-config.in";
Fix_Setup_Script
    "$ENV{GLOBUS_LOCATION}/setup/globus/setup-ssl-utils.pl";
Fix_Setup_Script
    "$ENV{GLOBUS_LOCATION}/setup/kca/setup-kca.pl";
Fix_Setup_Script
    "$ENV{GLOBUS_LOCATION}/setup/gsi_openssh_setup/setup-openssh.pl";

########################################
# Allow a specified command to be executed before running gpt-postinstall.
# This is used on AIX to run mds-aix-relink.sh, which works around some
# AIX-specific libtool problems.  It's a bit of a kludge, not to be used
# unless it's necessary.  Just to make it even kludgier, any occurrence of
# the string BUILD_DIR is replaced with the name of the build directory.
########################################
if ($Postinstall and defined $Config{PRE_POSTINSTALL}) {
    my $pre_postinstall = $Config{PRE_POSTINSTALL};
    $pre_postinstall =~ s(BUILD_DIR)($working_dir/BUILD);
    my @fixup_command = split /\s+/, $pre_postinstall;
    Execute @As_Installer, @fixup_command;
}

########################################
# Run gpt-postinstall.
########################################
if ($Postinstall) {
    Execute $Using_RPM ? @As_Root : @As_Installer,
            "$Config{GPT_LOCATION}/sbin/gpt-postinstall";
}
else {
    push @manual_steps,
         "Run $Config{GPT_LOCATION}/sbin/gpt-postinstall\n";
}

########################################
# Configure batch jobmanager
########################################
if (defined $Config{BATCH_SYSTEM}) {
    Execute $Using_RPM ? @As_Root : @As_Installer,
        "$Config{GLOBUS_LOCATION}/setup/globus/setup-globus-gram-job-manager",
        "-type=$Config{BATCH_SYSTEM}";
    my $libexec = "$Config{GLOBUS_LOCATION}/libexec";
    push @manual_steps,
         "Fix $Config{GLOBUS_LOCATION}/libexec/" .
         "globus-script-$Config{BATCH_SYSTEM}-* if necessary\n";
}

########################################
# Run setup-gsi to create /etc/grid-security
# (but we're going to replace its contents in a moment).
########################################
if ($Startup) {
    Execute @As_Root,
            "$Config{GLOBUS_LOCATION}/setup/globus/setup-gsi";
}
else {
    push @manual_steps,
         "As root, run $Config{GLOBUS_LOCATION}/setup/globus/setup-gsi\n";
}

########################################
# Now copy files to /etc/grid-security.
########################################
if ($Startup) {
    if (defined $Config{GRID_MAPFILE}) {
        Backup '/etc/grid-security/grid-mapfile';
        Execute @As_Root,
                'cp', $Local_File{$Config{GRID_MAPFILE}},
                      '/etc/grid-security/grid-mapfile';
        chmod 0444, '/etc/grid-security/grid-mapfile';
    }
    else {
        push @manual_steps,
             "Create /etc/grid-security/grid-mapfile, " .
             "mode 444, owned by root\n";
    }

    if (defined $Config{HOST_CERT}) {
        Backup '/etc/grid-security/hostcert.pem';
        Execute @As_Root,
                'cp', $Config{HOST_CERT}, '/etc/grid-security/hostcert.pem';
        chmod 0444, '/etc/grid-security/hostcert.pem';
    }
    else {
        push @manual_steps,
             "Create /etc/grid-security/hostcert.pem, " .
             "mode 444, owned by root\n";
    }

    if (defined $Config{HOST_KEY}) {
        Backup '/etc/grid-security/hostkey.pem';
        Execute @As_Root,
                'cp', $Config{HOST_KEY}, '/etc/grid-security/hostkey.pem';
        chmod 0400, '/etc/grid-security/hostkey.pem';
    }
    else {
        push @manual_steps,
             "Create /etc/grid-security/hostkey.pem, " .
             "mode 400, owned by root\n";
    }

    my $ldap_dir = '/etc/grid-security/ldap';

    if (defined $Config{LDAP_CERT}) {
        if (not -d $ldap_dir) {
            print "% mkdir $ldap_dir\n";
            mkdir $ldap_dir, 0777 or die "$ldap_dir: $!\n";
            print "% chown $Config{INSTALLER_USER}:$Config{INSTALLER_GROUP} " .
                   "$ldap_dir\n";
            chown $installer_uid, $installer_gid, $ldap_dir
                or die "$ldap_dir: $!\n";
        }
        Backup "$ldap_dir/ldapcert.pem";
        Execute @As_Installer,
                'cp', $Config{LDAP_CERT},
                      "$ldap_dir/ldapcert.pem";
        chmod 0444, "$ldap_dir/ldapcert.pem";
    }
    else {
        push @manual_steps,
             "Optionally create $ldap_dir/ldapcert.pem, ", 
             "mode 444, owned by $Config{INSTALLER_USER}\n";
    }

    if (defined $Config{LDAP_KEY}) {
        if (not -d $ldap_dir) {
            #
            # This is duplicated code (see above).  A bad idea, but I
            # haven't had time to restructure it yet.
            #
            print "% mkdir $ldap_dir\n";
            mkdir $ldap_dir, 0777 or die "$ldap_dir: $!\n";
            print "% chown $Config{INSTALLER_USER}:$Config{INSTALLER_GROUP} " .
                   "$ldap_dir\n";
            chown $installer_uid, $installer_gid, $ldap_dir
                or die "$ldap_dir: $!\n";
        }
        Backup "$ldap_dir/ldapkey.pem";
        Execute @As_Installer,
                'cp', $Config{LDAP_KEY},
                      "$ldap_dir/ldapkey.pem";
        chmod 0400, "$ldap_dir/ldapkey.pem";
    }
    else {
        push @manual_steps,
             "Optionally create $ldap_dir/ldapkey.pem, ", 
             "mode 400, owned by $Config{INSTALLER_USER}\n";
    }
}
else {
    push @manual_steps,
         "Set up /etc/grid-security directory.\n" .
         "Create /etc/grid-security/grid-mapfile,      mode 444, " .
         "owned by root\n" .
         "Create /etc/grid-security/hostcert.pem,      mode 444, " .
         "owned by root\n" .
         "Create /etc/grid-security/hostkey.pem,       mode 400, " .
         "owned by root\n" .
         "Create /etc/grid-security/ldap/ldapcert.pem, mode 444, " .
         "owned by $Config{INSTALLER_USER} (optional)\n" .
         "Create /etc/grid-security/ldap/ldapkey.pem,  mode 400, " .
         "owned by $Config{INSTALLER_USER} (optional)\n";
}

if ($Startup and defined $Config{CA_CERT_TARBALL}) {
    my $ca_cert_dir = '/etc/grid-security/certificates';
    Backup $ca_cert_dir;
    print "% mkdir $ca_cert_dir\n";
    mkdir $ca_cert_dir, 0777 or die "$ca_cert_dir: $!\n";
    print "% cd $ca_cert_dir\n";
    chdir $ca_cert_dir or die "$ca_cert_dir: $!\n";
    Execute @As_Root, 'tar', 'zxf', $Local_File{$Config{CA_CERT_TARBALL}};
}
else {
    push @manual_steps,
         "Copy CA certificates and signing-policy files to\n" .
         "/etc/grid-security/certificates\n";
}

if (defined $Sys{NET_DAEMON} and $Sys{NET_DAEMON} eq 'xinetd') {
    my $xinetd_dir = '/etc/xinetd.d';
    if (not -d $xinetd_dir) {
        push @manual_steps,
             "You seem to be running xinetd, but there is " .
             "no $xinetd_dir directory.\n" .
             "Your humble installation script is confused.\n" .
             "You're on your own.\n";
    }
    else {
        if ($Startup) {
            my $file1 = "$xinetd_dir/gsigatekeeper";
            Backup $file1;
            open FILE, "> $file1" or die "${file1}: $!\n";
            print FILE Inet_Config_Text 'xinetd', 'gsigatekeeper';

            my $file2 = "$xinetd_dir/gsiftp";
            Backup $file2;
            open FILE, "> $file2" or die "${file2}: $!\n";
            print FILE Inet_Config_Text 'xinetd', 'gsiftp';
            Execute @As_Root, qw(/etc/init.d/xinetd reload);
        }
        else {
            push @manual_steps,
                 "Set up xinetd scripts for gsigatekeeper and gsiftp\n";
        }
    }
}
elsif (defined $Sys{NET_DAEMON} and $Sys{NET_DAEMON} eq 'inetd') {
    #
    # For inetd, we need to update /etc/inetd.conf.
    # Since this involves editing an existing file (which requires
    # some care about its previous contents), and some sites might
    # want to use things like tcpd, we'll let the user do this manually.
    #
    my $update = "Update /etc/inetd.conf:\n";

    $update .= "gsigatekeeper:\n";
    $update .= "gsigatekeeper   stream tcp nowait root " .
               "$Config{GLOBUS_LOCATION}/sbin/globus-gatekeeper " .
               "globus-gatekeeper " .
               "-conf $Config{GLOBUS_LOCATION}/etc/globus-gatekeeper.conf\n";

    $update .= "gsiftp:\n";
    $update .= "gsiftp  stream  tcp     nowait  root    " .
               "$Config{GLOBUS_LOCATION}/sbin/in.ftpd in.ftpd -l -a\n";

    $update .= "Allow for any site-specific conventions for /etc/inetd.conf\n";

    $update .= "Execute as root \"$Sys{NET_DAEMON_RELOAD}\"\n";
    push @manual_steps, $update;
}
elsif (defined $Sys{NET_DAEMON}) {
    die "Internal error \$Sys{NET_DAEMON} = \"$Sys{NET_DAEMON}\"";
}
else {
    push @manual_steps,
         "Unable to determine net daemon (inetd or xinetd)\n" .
         "Arrange for gsigatekeeper, gsiftp, and other daemons to execute\n";
}

if ($Startup) {
    Execute @As_Installer,
            "$Config{GLOBUS_LOCATION}/sbin/SXXgris", 'start';
}
else {
    push @manual_steps,
         "As $Config{INSTALLER_USER} run " .
         "$Config{GLOBUS_LOCATION}/sbin/SXXgris start\n";
}

push @manual_steps,
     "Arrange for \"$Config{GLOBUS_LOCATION}/sbin/SXXgris start\"\n" .
     "to be executed by user \"$Config{INSTALLER_USER}\" on system boot\n";

push @manual_steps,
     "Start any other desired services\n";

push @manual_steps,
     "Configure to report to a GIIS server if desired\n";

chdir '/tmp';
if ($Cleanup) {
    Execute @As_Root, 'rm', '-rf', $working_dir;
}

########################################################################
# Some things can't be easily automated, some things shouldn't be
# automated, and some things I just haven't gotten around to automating
# yet.  Tell the user what still needs to be done manually.  All these
# things are collected and printed in one well-marked block at the end
# of the log.
########################################################################
my $text = join "\n", ">>> To be done manually:\n", @manual_steps;
my $stars = '*' x 72;
$text =~ s/^/* /gm;
$text = "$stars\n" .
        $text .
        "$stars\n" .
        Time_Image(time) . "\n";

print $text;
open INFO, ">>$info_file";
print INFO $text;
close INFO;

########################################################################
############################## Subroutines #############################
########################################################################

#
# Extract the actual revision number from an RCS revision string.
# The code is a little convoluted to prevent RCS from stepping on
# the search pattern.
#
sub Strip_Revision_String($) {
    my($revision) = @_;
    my $rev_tag = '\$' . 'Revision:';
    if ($revision =~ /^$rev_tag ([\d.]+) \$$/) {
        return $1;
    }
    else {
        return $revision;
    }
} # Strip_Revision_String

# ----------------------------------------------------------------------

#
# Called by Get_Config.
# Return an error message for a given filename or URL.
# Return undef if there's no problem.
#
sub File_Or_URL_Error(@) {
    my $varname = undef;
    my $url_ok = undef;
    my $executable = undef;
    my $name = undef;

    foreach my $arg (@_) {
        if ($arg =~ /^-varname=(.*)$/) {
            $varname = $1;
        }
        elsif ($arg eq '-url-ok') {
            $url_ok = 1;
        }
        elsif ($arg eq '-executable') {
            $executable = 1;
        }
        elsif ($arg =~ /^-/) {
            die "Internal error, File_Or_URL_Error, arg \"$arg\"\n";
        }
        else {
            $name = $arg;
        }
    }

    my $protocol = URL_Protocol $name;
    if (defined $protocol) {
        if ($url_ok) {
            if ($protocol eq 'http' or $protocol eq 'ftp') {
                #
                # This will be checked via Check_URLs after Get_Config
                # is finished (to guarantee that we know where the "wget"
                # command is).
                #
                return undef;
            }
            else {
                return "$varname URL \"$name\", unsupported protocol\n";
            }
        }
        else {
            return "$varname file \"$name\", URL not allowed\n";
        }
    }
    else {
        if ($name !~ /^\//) {
            return "$varname file \"$name\", relative path not allowed\n";
        }
        elsif (not -e $name) {
            return "$varname file \"$name\" does not exist\n";
        }
        elsif (not -f _) {
            return "$varname file \"$name\" is not an ordinary file\n";
        }
        elsif (not $executable and not -r _) {
            return "Can't read $varname file \"$name\"\n";
        }
        elsif (-z _) {
            return "$varname file \"$name\" is empty\n";
        }
        elsif ($executable and not -x _) {
            return "$varname file \"$name\" is not executable\n";
        }
        else {
            return undef;
        }
    }
} # File_Or_URL_Error

# ----------------------------------------------------------------------

#
# Returns the next line from the file.
# A trailing '\', possibly followed by whitespace, indicates a line
# continuation.
# Comments (introduced by '#') are stripped.
# Leading and trailing whitespace are stripped.
#
# The '\' may be followed by whitespace, but not by a comment.
# Any '#' character introduces a comment; there is no provision for
# escaping a '#' character.
# 
sub Next_Line($) {
    my($file) = @_;

    my $result = <$file>;
    return $result if not defined $result; # end of file

    while ($result =~ /\\\s*$/) {
        chomp $result;
        $result =~ s/\\\s*$//;
        my $contin = <$file>;
        if (defined $contin) {
            $result .= $contin;
        }
        else {
            die "Last line ends with '\\'\n";
        }
    }
    $result =~ s/#.*$//;
    $result =~ s/^\s+//;
    $result =~ s/\s+$//;
    return $result;
} # Next_Line;

# ----------------------------------------------------------------------

#
# Read and parse the config file (name given on the command line) and
# return the results as a hash (to be assigned to the global %Config).
#
# An additional hash entry, 'urls', is a reference to an array of any
# URLs specified in the config file.
#
sub Get_Config($) {
    my($config_file) = @_;
    my @errors = ();
    my @urls = ();
    my $default_prefix = undef;

    #
    # %Var_Info is a hash of hash references.
    # Each element is basically a declaration for a configuration variable.
    # This information is used for validity checking, and is local
    # to Get_Config.
    #
    my %Var_Info = (
        CONFIG_FILE_REVISION =>
            { type => 'string', required => 0 },
        TEMP_DIR =>
            { type => 'existing_directory', required => 0, default => '/tmp' },
        DOWNLOAD_DIR =>
            { type => 'existing_directory', required => 0 },
        WGET =>
            { type => 'executable_file', required => 0 },
        PATH =>
            { type => 'path', required => 1 },
        ENV =>
            { type => 'array_of_string', required => 0 },
        INSTALLER_USER =>
            { type => 'user_name', required => 0, default => 'globus' },
        INSTALLER_GROUP =>
            { type => 'group_name', required => 0, default => 'globus' },
        USE_SDSC_SU =>
            { type => 'boolean', required => 0, default => 0 },
        UMASK =>
            { type => 'umask', required => 0, default => '022' },
        GPT_TARBALL =>
            { type => 'file_or_url', required => 1 },
        BUNDLE =>
            { type => 'array_of_file_or_url', required => 1 },
        UPDATE =>
            { type => 'array_of_file_or_url', required => 0 },
        PRE_POSTINSTALL =>
            { type => 'string', required => 0 },
        GPT_LOCATION =>
            { type => 'new_directory', required => 0 },
        GLOBUS_LOCATION =>
            { type => 'new_directory', required => 1 },
        GLOBUS_VAR_DIRECTORY =>
            { type => 'new_directory', required => 0 },
        GLOBUS_HOSTNAME =>
            { type => 'string', required => 0 },
        GRID_MAPFILE =>
            { type => 'file_or_url', required => 0 },
        HOST_CERT =>
            { type => 'file', required => 0 },
        HOST_KEY =>
            { type => 'file', required => 0 },
        LDAP_CERT =>
            { type => 'file', required => 0 },
        LDAP_KEY =>
            { type => 'file', required => 0 },
        BATCH_SYSTEM =>
            { type => 'batch_system', required => 0 },
        CA_CERT_TARBALL =>
            { type => 'file_or_url', required => 0 }
    );

    print "Getting configuration from $config_file\n";

    #
    # Read config variables from config file.
    # Check for duplicate entries, unrecognized names, and syntax errors.
    #
    open CONFIG, $config_file or die "$config_file: $!\n";
    while (defined ($_ = Next_Line 'CONFIG')) {
        next if /^$/;
        if (/^(\.?\w+)\s+(.*)$/) {
            my ($name, $value) = ($1, $2);

            #
            # .PREFIX is not a true configuration option.  Instead, it
            # allows a default directory prefix to be set for options that
            # specify file names or URLs.  The default prefix, if any,
            # is prepended, with a '/' character, to any following file
            # or directory specifications.  It does not apply to options
            # given as absolute file names (starting with '/') or as URLs.
            #
            # This is in effect a limited macro facility, intended
            # to avoid extremely long lines in configuration files.
            # No checking is done on the prefix; the only checking is
            # on actual options after they're expanded.
            #
            # This expansion is performed only for options of type 'file',
            # 'file_or_url', and 'array_of_file_or_url'.
            # 
            if ($name eq '.PREFIX') {
                $default_prefix = $value;
            }
            elsif (defined $Var_Info{$name}) {
                my $Var = $Var_Info{$name};

                if ($Var->{type} eq 'file' or
                    $Var->{type} eq 'file_or_url' or
                    $Var->{type} eq 'array_of_file_or_url')
                {
                    #
                    # Prepend the default prefix (if any) only if the
                    # value is a relative pathname.
                    #
                    if (not defined URL_Protocol $value and
                        $value !~ /^\// and
                        defined $default_prefix)
                    {
                        $value = "$default_prefix/$value";
                    }
                    #
                    # Bundles and updates may have additional attributes
                    # specified on the same line.  Recognized attributes
                    # are:
                    #     kind=binary
                    #     kind=source
                    #     kind=rpm
                    #     flavor=<string>
                    #     option=<string>
                    # Multiple flavors may be specified, separated by
                    # commas, e.g.:
                    #     flavor=gcc32dbg,gcc32
                    # Options are passed to gpt-install or gpt-build.
                    # Multiple options may be specified, separated by
                    # commas, e.g.:
                    #     -update,-std-flavors
                    #
                    if ($name eq 'BUNDLE' or $name eq 'UPDATE') {
                        my $filename = undef;
                        my $kind     = undef;
                        my $flavor   = undef;
                        my $option   = undef;

                        foreach my $field (split /\s+/, $value) {
                            if (lc $field =~ /^kind=(.*)$/) {
                                if (defined $kind) {
                                    push @errors, "Duplicate kind, line $.\n";
                                }
                                else {
                                    if ($1 eq 'binary' or
                                        $1 eq 'source' or
                                        $1 eq 'rpm')
                                    {
                                        $kind = $1;
                                    }
                                    else {
                                        push @errors,
                                             "Unrecognized kind, line $., " .
                                             "should be \"binary\", " .
                                             "\"source\", or \"rpm\"\n";
                                    }
                                }
                            }
                            elsif (lc $field =~ /^flavor=(.*)$/) {
                                if (defined $flavor) {
                                    push @errors,
                                         "Duplicate flavor, line $." .
                                         " (use \"flavor=$flavor,$1\")\n";
                                }
                                else {
                                    $flavor = $1;
                                }
                            }
                            elsif (lc $field =~ /^option=(.*)$/) {
                                if (defined $option) {
                                    push @errors,
                                         "Duplicate option, line $." .
                                         " (use \"option=$option,$1\")\n";
                                }
                                else {
                                    $option = $1;
                                }
                            }
                            elsif ($field =~ /^(\w+)=/) {
                                push @errors,
                                     "Unrecognized attribute \"$1\", line $.\n";
                            }
                            else {
                                if (defined $filename) {
                                    push @errors,
                                         "Duplicate name, line $." .
                                         " (possible space in filename?)\n";
                                }
                                else {
                                    $filename = $field;
                                }
                            }
                            $value = $filename;
                            $Bundle_Kind{$value}   = $kind   if defined $kind;
                            $Bundle_Option{$value} = $option if defined $option;
                            $Bundle_Flavor{$value} = $flavor if defined $flavor;
                            $Is_Update{$value}     = 1 if $name eq 'UPDATE';
                        }
                    }
                }
                if ($Var->{type} =~ /^array_of/) {
                    push @{$Var->{value}}, $value;
                }
                else {
                    if (defined $Var->{value}) {
                        push @errors,
                             "Duplicate entry for \"$name\" at line $.\n";
                    }
                    else {
                        $Var->{value} = $value;
                    }
                }
            }
            else {
                push @errors, "Unrecognized option \"$name\" at line $.\n"
            }
        }
        else {
            push @errors, "Syntax error at line $.\n";
        }
    }
    close CONFIG;

    #
    # Validate each variable, depending on its type.
    #
    VAR:
    foreach my $varname (sort keys %Var_Info) {
        my $var = $Var_Info{$varname};
        if (not defined $var->{value}) {
            if ($var->{required}) {
                push @errors, "Missing option $varname\n";
                next VAR;
            }
            elsif (defined $var->{default}) {
                $var->{value} = $var->{default};
            }
            else {
                $var->{value} = undef;
                next VAR;
            }
        }

        #
        # An arbitrary string.
        #
        if ($var->{type} eq 'string') {
            #
            # Nothing to validate
            #
        }

        #
        # An array of arbitrary strings.
        #
        elsif ($var->{type} eq 'array_of_string') {
            #
            # Nothing to validate
            #
        }

        #
        # An existing directory.
        # It must be readable, writable, and searchable.
        #
        elsif ($var->{type} eq 'existing_directory') {
            if ($var->{value} !~ /^\//) {
                push @errors, "$varname directory \"$varname\", " .
                              "relative path not allowed\n";
            }
            if (not (-d $var->{value} and -r _ and -w _ and -x _)) {
                push @errors, "Bad $varname directory \"$var->{value}\"\n";
            }
        }

        #
        # A directory to be created.
        # If it already exists, it must be empty.
        #
        elsif ($var->{type} eq 'new_directory') {
            my $dir = $var->{value};
            if ( $AIX_Kludge and
                 ( ( $varname eq 'GPT_LOCATION' ) or
                   ( $varname eq 'GLOBUS_LOCATION' and 
                     ( $Var_Info{GPT_LOCATION}->{value} eq
                       $Var_Info{GLOBUS_LOCATION}->{value} ) ) ) )
            {
                # Ok for directory to exist.
            }
            else {
                if (not -e $dir) {
                    # ok
                }
                elsif (-d $dir) {
                    if (Is_Empty_Dir $dir) {
                        # ok
                    }
                    else {
                        push @errors, "$varname \"$dir\" is not empty\n";
                    }
                }
                else {
                    push @errors, "$varname \"$dir\" is not a directory\n";
                }
            }
        }

        #
        # A path, consisting of a colon-delimited list of directories.
        # Each directory must exist, and must be readable and searchable.
        #
        elsif ($var->{type} eq 'path') {
            foreach my $dir (split /:/, $var->{value}) {
                if ($dir !~ /^\//) {
                    push @errors,
                         "Bad directory in ${varname}: \"$dir\"\n";
                }
                elsif (not -d $dir) {
                    push @errors,
                         "Nonexistent directory in ${varname}: \"$dir\"\n";
                }
                elsif (not -r $dir or not -x $dir) {
                    push @errors,
                         "Bad permissions on directory in " .
                         "${varname}: \"$dir\"\n";
                }
            }
        }

        #
        # A non-root Unix user name.
        #
        elsif ($var->{type} eq 'user_name') {
            my @pw = getpwnam $var->{value};
            if (not defined $pw[2]) {
                push @errors, "Bad $varname \"$var->{value}\", no such user\n";
            }
            elsif ($pw[2] <= 0) {
                push @errors, "Bad $varname \"$var->{value}\", uid = $pw[2]\n";
            }
        }

        #
        # A non-root Unix group name.
        #
        elsif ($var->{type} eq 'group_name') {
            my @gr = getgrnam $var->{value};
            if (not defined $gr[2]) {
                push @errors, "Bad $varname \"$var->{value}\", no such group\n";
            }
            elsif ($gr[2] <= 0) {
                push @errors, "Bad $varname \"$var->{value}\", gid = $gr[2]\n";
            }
        }

        #
        # A umask, a sequence of one or more octal digits.
        #
        elsif ($var->{type} eq 'umask') {
            if ($var->{value} !~ /^[0-7]+$/) {
                push @errors, "Bad $varname \"$var->{value}\"\n";
            }
        }

        #
        # A Boolean value.
        # False may be represented as '0', 'no', or 'false'.
        # True may be represented as '1', 'yes', or 'true'.
        # Value is case-insensitive.
        #
        elsif ($var->{type} eq 'boolean') {
            if ($var->{value} eq '0' or
                lc $var->{value} eq 'no' or
                lc $var->{value} eq 'false')
            {
                $var->{value} = 0;
            }
            elsif ($var->{value} eq '1' or
                lc $var->{value} eq 'yes' or
                lc $var->{value} eq 'true')
            {
                $var->{value} = 1;
            }
            else {
                push @errors, "$varname must be one of the following:\n" .
                              "    0, no, false, 1, yes, true\n";
            }
        }

        #
        # A filename or a URL.
        # See File_Or_URL_Error().
        # 
        elsif ($var->{type} eq 'file_or_url') {
            my $error = File_Or_URL_Error
                            "-varname=$varname", "-url-ok", $var->{value};
            push @errors, $error if defined $error;
            if (defined URL_Protocol $var->{value}) {
                push @urls, $var->{value};
            }
        }

        elsif ($var->{type} eq 'array_of_file_or_url') {
            foreach my $item (@{$var->{value}}) {
                my $error = File_Or_URL_Error
                                "-varname=$varname", "-url-ok", $item;
                push @errors, $error if defined $error;
                if (defined URL_Protocol $var->{value}) {
                    push @urls, $var->{value};
                }
            }
        }

        elsif ($var->{type} eq 'file') {
            my $error = File_Or_URL_Error "-varname=$varname", $var->{value};
            push @errors, $error if defined $error;
        }

        elsif ($var->{type} eq 'executable_file') {
            my $error = File_Or_URL_Error
                            "-varname=$varname", "-executable", $var->{value};
            push @errors, $error if defined $error;
        }

        elsif ($var->{type} eq 'batch_system') {
            $var->{value} = lc $var->{value};

            my @batch_systems = qw( condor easymcs fork glunix grd
                                    loadleveler lsf nqe nswc pbs pexec
                                    prun );
            my %is_batch_system = map { ( $_ => 1 ) } @batch_systems;
            if (not $is_batch_system{$var->{value}}) {
                push @errors, "$varname must be one of:\n" .
                              "    @batch_systems\n";
            }

        }

        else {
            #
            # The missing "\n" in the error message is deliberate.
            # It causes Perl to print more information about the location
            # of the error.
            #
            die "Internal error, bad variable type \"$var->{type}\"";
        }

    }

    #
    # If GPT_LOCATION is not specified, set it to the same directory
    # as GLOBUS_LOCATION (which has already been validated above).
    #
    if (not defined $Var_Info{GPT_LOCATION}->{value}) {
        $Var_Info{GPT_LOCATION}->{value} = $Var_Info{GLOBUS_LOCATION}->{value};
    }

    if (defined $Var_Info{CONFIG_FILE_REVISION}->{value}) {
        $Var_Info{CONFIG_FILE_REVISION}->{value} =
            Strip_Revision_String $Var_Info{CONFIG_FILE_REVISION}->{value};
    }

    #
    # Special check: GLOBUS_VAR_DIRECTORY may not be a subdirectory
    # of GLOBUS_LOCATION.
    #
    if (defined $Var_Info{GLOBUS_VAR_DIRECTORY}->{value}) {
        my $globus_dir = $Var_Info{GLOBUS_LOCATION}->{value};
        my $var_dir = $Var_Info{GLOBUS_VAR_DIRECTORY}->{value};
        if ($var_dir =~ /^\Q$globus_dir\E\//) {
            push @errors, "GLOBUS_VAR_DIRECTORY may not be a subdirectory of " .
                          "GLOBUS_LOCATION\n" .
                          "To use \$GLOBUS_LOCATION/var, don't specify " .
                          "GLOBUS_VAR_DIRECTORY.\n";
        }
    }

    if (@errors) {
        my $Error_s = (scalar @errors == 1 ? 'Error' : 'Errors');
        die ">>> $Error_s in config file ${config_file}:\n",
            @errors;
    }

    my %Result = ();

    foreach my $varname (keys %Var_Info) {
        $Result{$varname} = $Var_Info{$varname}->{value};
    }
    if (@urls) {
        $Result{urls} = \@urls;
    }

    foreach my $key (sort keys %Result) {
        my $value = $Result{$key};
        if (ref $value eq 'ARRAY') {
            printf "    %-20s => {\n", $key;
            foreach my $elem (@$value) {
                my @fields = ("\"$elem\"");
                if ($key ne 'urls') {
                    if (defined $Bundle_Kind{$elem}) {
                        push @fields, "kind=$Bundle_Kind{$elem}";
                    }
                    if (defined $Bundle_Flavor{$elem}) {
                        push @fields, "flavor=$Bundle_Flavor{$elem}";
                    }
                }
                print "        @fields\n";
            }
            print "    }\n";
        }
        else {
            printf "    %-20s => %s",
                   $key,
                   defined $value ? "\"$value\"" : 'undef';
            #
            # Currently, attributes can only be defined for BUNDLE,
            # which is an array.
            #
            print "\n";
        }
    }

    return %Result;
} # Get_Config

# ----------------------------------------------------------------------

#
# Collect system information and return it in a hash.
#
sub Get_System_Information() {
    my %Result = ();

    chomp($Result{OSNAME}  = `uname -s`);
    chomp($Result{MACHINE} = `uname -m`);
    $Result{LC_OSNAME}  = lc $Result{OSNAME};
    $Result{LC_MACHINE} = lc $Result{MACHINE};

    if ($Result{LC_MACHINE} eq 'ia64') {
        $Result{BITS} = 64;
    }
    else {
        $Result{BITS} = 32;
    }

    #
    # Detect whether we're running inetd or xinetd, and what its PID is.
    #
    my $ps_command;

    if ($Result{LC_OSNAME} eq 'darwin' or $Result{LC_OSNAME} =~ /bsd/) {
        $ps_command = 'ps -u -U root';
    }
    else {
        $ps_command = 'ps -f -u root';
    }

    my @inetd_processes = ();
    my @xinetd_processes = ();

    open PIPE, "$ps_command |";
    while (<PIPE>) {
        chomp;
        s/^\s*//;
        my @fields = split;
        my $pid = $fields[1];
        if (/\binetd\b/) {
            push @inetd_processes, $pid;
        }
        elsif (/\bxinetd\b/) {
            push @xinetd_processes, $pid;
        }
    }
    close PIPE;

    if (scalar @inetd_processes == 1 and scalar @xinetd_processes == 0) {
        $Result{NET_DAEMON} = 'inetd';
        $Result{NET_DAEMON_PID} = $inetd_processes[0];
        if (-x '/etc/init.d/inet') {
            $Result{NET_DAEMON_RELOAD} = '/etc/init.d/inet reload';
        }
        elsif (-x '/etc/rc.d/init.d/inet') {
            $Result{NET_DAEMON_RELOAD} = '/etc/rc.d/init.d/inet reload';
        }
        else {
            $Result{NET_DAEMON_RELOAD} = "kill -HUP $Result{NET_DAEMON_PID}";
        }
    }
    elsif (scalar @inetd_processes == 0 and scalar @xinetd_processes == 1) {
        $Result{NET_DAEMON} = 'xinetd';
        $Result{NET_DAEMON_PID} = $xinetd_processes[0];
        if (-x '/etc/init.d/xinetd') {
            $Result{NET_DAEMON_RELOAD} = '/etc/init.d/xinetd reload';
        }
        elsif (-x '/etc/rc.d/init.d/xinetd') {
            $Result{NET_DAEMON_RELOAD} = '/etc/rc.d/init.d/xinetd reload';
        }
        else {
            $Result{NET_DAEMON_RELOAD} = '';
        }
    }

    print "System Information:\n";
    foreach my $key (sort keys %Result) {
        my $value = $Result{$key};
        if (ref $value eq 'ARRAY') {
            printf "    %-20s => {\n", $key;
            foreach my $elem (@$value) {
                print "        \"$elem\"\n";
            }
            print "    }\n";
        }
        else {
            printf "    %-20s => \"%s\"\n", $key, $Result{$key};
        }
    }

    return %Result;
} # Get_System_Information

# ----------------------------------------------------------------------

#
# Read the DOWNLOAD_DIR and initialize the mapping of URLs to cached
# downloaded files.
#
sub Init_Download() {
    return if $Download_Initialized;

    die "No DOWNLOAD_DIR specified\n" if not defined $Config{DOWNLOAD_DIR};

    if (not defined $wget) {
        if (defined $Config{WGET}) {
            $wget = $Config{WGET};
        }
        else {
            DIR:
            foreach my $dir (split /:/, $ENV{PATH}) {
                if (-x "$dir/wget") {
                    $wget = "$dir/wget";
                    last DIR;
                }
            }
        }
    }

    die "No wget command found\n" if not defined $wget;

    my @subdirs = ();

    opendir DIR, $Config{DOWNLOAD_DIR} or die "$Config{DOWNLOAD_DIR}: $!\n";
    foreach my $subdir (sort readdir DIR) {
        next if $subdir !~ /^\d+$/;
        $download_index = $subdir + 1 if $subdir >= $download_index;
        my $url_file = "$Config{DOWNLOAD_DIR}/$subdir/.url";
        open URL, $url_file or next;
        my $url = <URL>;
        close URL;
        chomp $url;
        $Local_File{$url} = "$Config{DOWNLOAD_DIR}/$subdir/" .
                            File::Basename::basename $url;
    }
    closedir DIR;

    $Download_Initialized = 1;
} # Init_Download

# ----------------------------------------------------------------------

#
# Returns the protocol of a URL, generally 'http' or 'ftp'.
# If the argument is not a URL, returns undef.
#
sub URL_Protocol($) {
    if ($_[0] =~ m<^(\w+)://>) {
        return $1;
    }
    else {
        return undef;
    }
} # URL_Protocol

# ----------------------------------------------------------------------

#
# Given a list of URLs, verify that each one exists.
# Uses "wget --spider".
#
sub Check_URLs(@) {
    Init_Download;
    die "No wget command found or specified\n" if not defined $wget;
    my @bad_urls = ();

    foreach my $url (@_) {
        my $result = system $wget, '--spider', '-o', '/dev/null', $url;
        if ($result != 0) {
            push @bad_urls, $url;
        }
    }

    if (@bad_urls) {
        my $message;
        if (scalar @bad_urls == 1) {
            $message = ">>> The following URL does not exist:\n";
        }
        else {
            $message = ">>> The following URLs do not exist:\n";
        }
        foreach my $url (@bad_urls) {
            $message .= "    $url\n";
        }
        die $message;
    }
} # Check_URLs

# ----------------------------------------------------------------------

#
# Download any files specified by an "http://" or "ftp://" URL.
# Update %Local_File entries to point to downloaded files.
#
# Arguments are file names or URLs.
#
sub Download_Files(@) {
    my @names = @_;
    my $changed_dir = 0;

    foreach my $name (@names) {
        if (defined URL_Protocol $name) {
            Init_Download;

            my $target_file;

            if (not defined $Local_File{$name}) {
                my $target_dir = sprintf "%s/%03d",
                                         $Config{DOWNLOAD_DIR},
                                         $download_index++;
                $target_file = "$target_dir/" .
                               File::Basename::basename $name;

                print "% mkdir $target_dir\n";
                mkdir $target_dir, 0777 or die "$target_dir: $!\n";
                chown $installer_uid, $installer_gid, $target_dir
                    or die "$target_dir: $!\n";

                print "% cd $target_dir\n";
                chdir $target_dir or die "$target_dir: $!\n";
                $changed_dir = 1;

                Execute @As_Installer,
                        'wget', '-o', '/dev/null', $name;
                if (not -e $target_file) {
                    die "Download failed for $target_file (no file)\n";
                }
                elsif (-s $target_file == 0) {
                    die "Download failed for $target_file (empty file)\n";
                }
                open URL, ">$target_dir/.url" or die "$target_dir/.url: $!\n";
                print URL "$name\n";
                close URL;
                chown $installer_uid, $installer_gid, "$target_dir/.url";
                #
                # Ignore any errors from chown (no biggie if it fails).
                #
            }
        }
        else {
            $Local_File{$name} = $name;
        }
    }

    if ($changed_dir) {
        print "% cd $working_dir\n";
        chdir $working_dir or die "$working_dir: $!\n";
    }
} # Download_Files

# ----------------------------------------------------------------------

#
# Given a number of seconds (possibly fractional), return a readable
# string, similar to the format used by the csh "time" builtin command.
#
sub Elapsed_Time_Image($) {
    my($seconds) = @_;

    my $h = int($seconds / 3600);
    $seconds -= 3600 * $h;      # Can't use "%"; it truncates.

    my $m = int($seconds / 60);
    $seconds -= 60 * $m;

    if ($h > 0) {
        return sprintf '%d:%02d:%05.2f', $h, $m, $seconds;
    }
    else {
        return sprintf '%d:%05.2f', $m, $seconds;
    }
} # Elapsed_Time_Image

# ----------------------------------------------------------------------

#
# Given a raw timestamp, return a human-readable string.
# Time zone is UTC, format is basically ISO-8601.
#
sub Time_Image($) {
    my($time) = @_;
    my($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst)
        = gmtime time;
    return sprintf "%s %04d-%02d-%02d %02d:%02d:%02d UTC",
                   (qw(Sun Mon Tue Wed Thu Fri Sat))[$wday],
                   1900+$year, 1+$mon, $mday, $hour, $min, $sec;
} # Time_Image

# ----------------------------------------------------------------------

#
# Create a unique temporary file and return its name.
#
sub Make_Temp_File($) {
    my($content) = @_;
    my $filename = sprintf "$working_dir/input-%03d",
                           $temp_file_serial_number ++;
    open TMP, ">$filename" or die "${filename}: $!\n";
    print TMP $content;
    close TMP;
    return $filename;
} # Make_Temp_File

# ----------------------------------------------------------------------

#
# Given a numeric value returned by system(), interpret it
# and return the result as a hash reference.
# Die if result indicates failure and second argument (check) is true.
# Print a message on failure if check is false.
#
# Note: die'ing and/or printing a message from this routine is a bit ugly,
# but it's useful in the context of this program (gpt-wizard).
#
sub Get_Result($$) {
    my ($status, $check) = @_;
    my $Result = {};
    my $hi_8 = ($status & 0xff00) >> 8;
    my $lo_8 =  $status & 0x00ff;

    my $WSTOPFLG = 0x007f;
    my $WCOREFLG = 0x0080;

    if ($lo_8 == $WSTOPFLG) {
        #
        # Stopped by signal
        #
        $Result->{signal} = $hi_8;
        $Result->{stopped} = 1;
        $Result->{desc} = "stopped, signal $hi_8";
        if ($check) {
            die "$Result->{desc}\n";
        }
        else {
            print "[$Result->{desc}]\n";
        }
    }
    elsif ($lo_8 == 0) {
        #
        # Normal exit() (possibly with non-zero status)
        #
        $Result->{status} = $hi_8;
        $Result->{desc} = "exit $hi_8";
        if ($Result->{status} != 0) {
            if ($check) {
                die "$Result->{desc}\n";
            }
            else {
                print "[$Result->{desc}]\n";
            }
        }
    }
    elsif ($hi_8 == 0) {
        #
        # Killed by signal
        #
        my $signal = $lo_8;
        if ($signal & $WCOREFLG) {
            $signal &= ~$WCOREFLG;
            $Result->{core} = 1;
        }
        $Result->{signal} = $signal;
        $Result->{desc} = "signal $signal";
        if ($Result->{core}) {
            $Result->{desc} .= " (core dumped)";
        }
        if ($check) {
            die "$Result->{desc}\n"
        }
        else {
            print "[$Result->{desc}]\n";
        }
    }
    return $Result;
} # Get_Result

# ----------------------------------------------------------------------

sub Get_Update_Flavors($) {
    my($filename) = @_;

    my $module_name = File::Basename::basename $filename;
    #
    # File name format is name-version.tar.gz
    #
    $module_name =~ s/-[^-]*\.tar\.gz$//;

    my %flavor = ();

    open PIPE, "$ENV{GLOBUS_LOCATION}/sbin/gpt-query $module_name |";
    while (<PIPE>) {
        chomp;
        next if not /ver:/;
        #
        # Strip leading whitespace and grab first word.
        #
        s/^\s*//;
        s/\s.*$//;
        #
        # e.g., "globus_openssl_module-gcc32dbg-dev".
        # Split on /-/ and grab next-to-last field.
        #
        my @fields = split /-/, $_;
        if (scalar @fields >= 2) {
            my $new_flavor = $fields[$#fields - 1];
            $flavor{$new_flavor} = 1;
        }
    }
    close PIPE;

    my @flavors = sort keys %flavor;
    if (@flavors) {
        return grep { $_ ne 'noflavor' } @flavors;
    }
    else {
        die "Unable to determine flavors for $filename\n";
    }
} # Get_Update_Flavors

# ----------------------------------------------------------------------

#
# Execute a command with arguments.
# The command name is preceded by zero or more options starting with '-'.
#
# -verbose      Print command to stdout before executing.
# -time         Like csh "time" command
# -time=N       Show time only if command took at least N wall seconds
# -background   Run command in background; don't wait for completion.
# -check        Die if command fails.
# -user=name    Execute as specified user (and group).
#               Name can be "user", "user.group", or "user:group".
# -stdin=file   Redirect stdin from specified file.
# -input=string Redirect stdin from temp file with specified contents.
#
# 
sub Execute(@) {
    my @args = @_;
    my $Verbose = 0;
    my $Min_Time = undef;
    my $Background = 0;
    my $Check = 0;
    my $User = undef;
    my $Group = undef;
    my $Input_String = undef;
    my $Stdin = undef;
    my $Stdin_Is_Temporary = undef;
    my @before;
    my @after;
    my $system_result;

    Debug "Execute, scalar \@args = ", scalar @args, "\n";
    my @caller = caller;
    Debug "   Called from $caller[0]::$caller[1] line $caller[2]\n";
    #
    # Process arguments
    #
    while ( scalar @args > 0 && $args[0] =~ /^-/) {
        my $arg = shift @args;
        if ($arg eq '-verbose') {
            $Verbose = 1;
        }
        elsif ($arg eq '-time') {
            $Min_Time = 0;
        }
        elsif ($arg =~ /^-time=(\d+)$/) {
            $Min_Time = $1;
        }
        elsif ($arg eq '-background') {
            $Background = 1;
        }
        elsif ($arg eq '-check') {
            $Check = 1;
        }
        elsif ($arg =~ /^-user=(.+)$/) {
            $User = $1;
            if ($User =~ /^(.*)[.:](.*)$/) {
                $User = $1;
                $Group = $2;
            }
        }
        elsif ($arg =~ /^-stdin=(.+)$/) {
            $Stdin = $1;
        }
        elsif ($arg =~ /^-input=(.+)$/s) {
            #
            # Need to use "/s" modifier so "." matches "\n"
            #
            $Input_String = $1;
            $Stdin = Make_Temp_File $Input_String;
            $Stdin_Is_Temporary = 1;
        }
        else {
            die "Execute: Unrecognized argument: $arg\n";
        }
    }

    Debug "   args = @args\n";
    if (defined $User and not $Config{USE_SDSC_SU}) {
        my @pw = getpwnam $User;
        die "${User}: No such user\n" if not @pw;
        my @gr;
        if (defined $Group) {
            @gr = getgrnam $Group;
            die "${Group}: No such group\n" if not @gr;
        }
        my $uid = $pw[2];
        my $gid = defined $Group ? $gr[2] : $pw[3];
        my $pid;
        if (($pid = fork) == 0) {
            #
            # This is the child process.
            # Set the real and effective user (and group) id,
            # and continue to handle the call.
            #
            # To be honest, I don't quite understand the distinction
            # between "real" and "effective" user and group ids, so I
            # set both just to be sure.
            #
            # It may be possible to temporarily set the effective(?) user
            # and group ids, run the command, and set them back, without
            # forking a child process, but it doesn't seem to be possible
            # to do this in a portable and reliable manner.
            #
            # Set the group ids first; once we set the user ids, we're no
            # longer root and don't have permission to set the group ids.
            #
            # Setting $< and $( (setruid() and setrgid()) is not supported
            # on AIX; use eval to avoid blowing up on the attempt.
            #
            if (defined $gid) {
                eval { $( = $gid };
                $) = $gid;
            }
            eval { $< = $uid };
            $> = $uid;
            Debug "Child: \$< = $< \$> = $>\n";
            delete $SIG{__DIE__};
        }
        else {
            #
            # This is the parent process.
            # Wait for the child to finish.  
            #
            Debug "Parent calling waitpid\n";
            if (waitpid $pid, 0 == -1) {
                Debug ">>> waitpid returned -1, \$? = $?, \$! = $!\n",
                      "    Ignoring error (ick)\n";
                # die "waitpid ${pid}: $! (\$? = $?)\n";
            }
            Debug "Parent after waitpid, \$? = $?\n";
            return Get_Result $?, $Check;
        }
    }

    if (defined $Min_Time) {
        print Time_Image time, "\n";
    }

    if ($Verbose) {
        Debug "Building command_string, args = @args\n";
        my $command_string = '% ';
        $command_string .= "[$User] " if defined $User;
        $command_string .= "@args";
        $command_string .= " < $Stdin" if defined $Stdin;
        $command_string .= " &" if $Background;
        print "$command_string\n";
        if (defined $Input_String) {
            print "  (input is ", Image $Input_String, ")\n";
        }
    }

    if ($Background) {
        my $pid = fork;
        if ($pid != 0) {
            #
            # I'm the parent
            #
            my $Result = {};
            $Result->{pid} = $pid;
            return $Result;
        }
    }

    if (defined $Stdin) {
        open SAVE_STDIN, "<&STDIN";
        print SAVE_STDIN if 0;          # keep "perl -w" happy
        close STDIN;
        open STDIN, "<$Stdin" or die "${Stdin}: $!\n";
    }
    if (defined $Min_Time) {
        @before = POSIX::times;
    }
    if (defined $User and $Config{USE_SDSC_SU}) {
        #
        # Note: This ignores INSTALLER_GROUP
        #
        my @arguments = @args;
        foreach (@arguments) {
            $_ = "'$_'" if / /;
        }
        $system_result = system 'su', $User, '-c',
                         "umask $Config{UMASK} ; @arguments";
    }
    else {
        $system_result = system @args;
    }
    Debug "system_result = $system_result\n";
    if (defined $Min_Time) {
        @after = POSIX::times();
    }
    if (defined $Stdin) {
        open STDIN, "<&SAVE_STDIN";
        unlink $Stdin if $Stdin_Is_Temporary;
    }

    #
    # Emulate csh "time" command
    #
    if (defined $Min_Time) {
        my $elapsed_ticks    =  $after[0] - $before[0];
        my $user_CPU_ticks   = ($after[1] - $before[1]) +   # this process
                               ($after[3] - $before[3]);    # child processes
        my $system_CPU_ticks = ($after[2] - $before[2]) +   # this process
                               ($after[4] - $before[4]);    # child processes

        my $percent_usage;
        if ($elapsed_ticks == 0) {
            $percent_usage = 0.0;
        }
        else {
            $percent_usage = ($user_CPU_ticks + $system_CPU_ticks)
                             / $elapsed_ticks * 100;
        }

        my $elapsed_seconds    = $elapsed_ticks    / $CLK_TCK;
        my $user_CPU_seconds   = $user_CPU_ticks   / $CLK_TCK;
        my $system_CPU_seconds = $system_CPU_ticks / $CLK_TCK;

        if ($elapsed_seconds >= $Min_Time) {
            printf "%.2fu %.2fs %s %.1f%%\n",
                   $user_CPU_seconds,
                   $system_CPU_seconds,
                   Elapsed_Time_Image $elapsed_seconds,
                   $percent_usage;
        }
    }
    if ($Background) {
        exit;
    }
    elsif (defined $User and not $Config{USE_SDSC_SU}) {
        #
        # This is the child process; bail out and let the parent handle
        # the consequences.  Try to arrange for $? after the parent's
        # call to waitpid to match what it would have been if the parent
        # had called system.
        #
        my $Result = Get_Result $system_result, 0;
        if (defined $Result->{signal}) {
            Debug "Child: kill $Result->{signal}, $$\n";
            kill $Result->{signal}, $$;
        }
        else {
            Debug "Child: exit $Result->{status}\n";
            exit $Result->{status};
        }
    }

    return Get_Result $system_result, $Check;
} # Execute

# ----------------------------------------------------------------------

#
# Confirm that the necessary services are specified in
# /etc/services (or the NIS equivalent).
#
sub Check_Services() {
    my %expected_name = ( 2119 => 'gsigatekeeper',
                          2135 => 'gris',
                          2811 => 'gsiftp' );
    my @errors = ();
    foreach my $port (sort keys %expected_name) {
        my @serv = getservbyport $port, 'tcp';
        if (not @serv) {
            push @errors,
                 "    No service on port $port, " .
                 "expected $expected_name{$port}\n";
        }
        elsif ($serv[0] ne $expected_name{$port}) {
            push @errors,
                 "    Service on port $port is \"$serv[0]\", ",
                  "should be \"$expected_name{$port}\"\n";
        }
    }
    if (@errors) {
        push @manual_steps,
             "Correct /etc/services or equivalent:\n" .
             join('', @errors);
    }
} # Check_Services

# ----------------------------------------------------------------------

#
# Calls to die() cause this function to be called before exiting.
# Clean up temporary directory.
#
sub Die_Handler($) {
    if (defined $working_dir) {
        if ($Cleanup) {
            print ">>> Cleaning up\n";
            #
            # The working dir isn't necessarily under /tmp; we chdir there
            # so we aren't in a directory that we're about to nuke.
            #
            chdir '/tmp';
            Execute @As_Root, 'rm', '-rf', $working_dir;
        }
        else {
            print ">>> The working directory $working_dir\n",
                  "    has not been cleaned up.\n";
        }
    }
    print Time_Image time, "\n";
} # Die_Handler

# ----------------------------------------------------------------------

#
# Given a time, return an ASCII UTC timestamp suitable for use
# in a file name, such as "2002-08-16-183304".
#
sub Timestamp_Of($) {
    my($time) = @_;
    my ($sec, $min, $hour, $mday, $mon, $year, $wday, $yday, $isdst) =
        gmtime($time);
    return sprintf "%04d-%02d-%02d-%02d%02d%02d",
                   $year + 1900, $mon + 1, $mday, $hour, $min, $sec;
} # Timestamp_Of

# ----------------------------------------------------------------------

#
# Rename the specified file by appending its last modification time to
# its name, returning the name of the backup (which may be ignored if
# it's not needed).  If file doesn't exist, return undef.  May be used
# on directories as well as ordinary files.
#
sub Backup($) {
    my($filename) = @_;
    my @stat;

    return undef if not -e $filename;
    @stat = stat $filename or die "${filename}: $!\n";
    my $timestamp = Timestamp_Of $stat[9]; # mtime
    my $backup_name = "$filename-$timestamp";
    rename $filename, $backup_name or die "$backup_name: $!\n";
    return $backup_name;
} # Backup

# ----------------------------------------------------------------------

#
# Returns true iff the argument is an empty directory.
#
sub Is_Empty_Dir($) {
    my($dir) = @_;
    if (-d $dir) {
        opendir DIR, $dir or die "${dir}: $!\n";
        my @files = grep { $_ ne '.' and $_ ne '..' } readdir DIR;
        closedir DIR;
        if (@files) { return 0; } else { return 1; }
    }
    else {
        return 0;
    }
} # Is_Empty_Dir

# ----------------------------------------------------------------------

#
# Create a backup copy of the named file and edit it so it doesn't
# prompt for user input.  The required edits are determined by the name
# of the file.  This is *extremely* ad-hoc (but not quite as bad as
# guessing what input is expected).
#
sub Fix_Setup_Script($) {
    my($file) = @_;
    if (-e $file) {
        print "% Editing $file\n";
    }
    else {
        print ">>> Fix_Setup_Script: No file $file\n";
        return;
    }
    my $basename = File::Basename::basename $file;
    my $backup_file = Backup $file;

    open ORIG, $backup_file or die "$backup_file: $!\n";
    open NEW, ">$file" or die "${file}: $!\n";
    if ($basename eq 'grid-security-config.in') {
        while (<ORIG>) {
            s/\bread ans\b/ans="" # $&/;
            s/\bread selection\b/echo "q" ; selection="q" ; # $&/;
            print NEW;
        }
    }
    elsif ($basename eq 'setup-ssl-utils.pl') {
        while (<ORIG>) {
            s/^Press return to contine.*//;
            s/\$foo=<STDIN>;/# $&/;
            print NEW;
        }
    }
    elsif ($basename eq 'setup-kca.pl') {
        while (<ORIG>) {
            s/\$myincrement = <STDIN>;/print "0\\n"; \$myincrement = 0; # $&/;
            s/\$myserial = <STDIN>;/print "\\n"; \$myserial = ""; # $&/;
            s/\$resp = <STDIN>;/print "No\\n"; \$resp="No"; # $&/;
            print NEW;
        }
    }
    elsif ($basename eq 'setup-openssh.pl') {
        while (<ORIG>) {
            s/\$response = query_boolean/\$response = "y"; # $&/;
            s/print "Press <return>/# $&/;
            s/\$trash = <STDIN>/# $&/;
            print NEW;
        }
    }
    else {
        #
        # The lack of a newline in the following die() is intentional;
        # it causes Perl to print more information about the error.
        #
        die "Fix_Setup_Script: Unexpected file name \"$file\"";
    }
    close ORIG;
    close NEW;

    my @stat = stat $backup_file;
    my ($mode, $uid, $gid) = ($stat[2], $stat[4], $stat[5]);
    chmod $mode, $file;
    chown $uid, $gid, $file;
} # Fix_Setup_Script

# ----------------------------------------------------------------------

sub Inet_Config_Text($$) {
    my($daemon, $service) = @_;

    if ("$daemon/$service" eq 'xinetd/gsigatekeeper') {
        return <<"EOF";
service gsigatekeeper
{
    socket_type     = stream
    protocol        = tcp
    wait            = no
    user            = root
    env             = LD_LIBRARY_PATH=$Config{GLOBUS_LOCATION}/lib
    server          = $Config{GLOBUS_LOCATION}/sbin/globus-gatekeeper
    server_args     = -conf $Config{GLOBUS_LOCATION}/etc/globus-gatekeeper.conf
    disable         = no
}
EOF
    }
    elsif ("$daemon/$service" eq 'xinetd/gsiftp') {
        return <<"EOF";
service gsiftp
{
    instances       = 1000
    socket_type     = stream
    wait            = no
    user            = root
    env             = LD_LIBRARY_PATH=$Config{GLOBUS_LOCATION}/lib
    server          = $Config{GLOBUS_LOCATION}/sbin/in.ftpd
    server_args     = -l -a -G $Config{GLOBUS_LOCATION}
    log_on_success  += DURATION USERID
    log_on_failure  += USERID
    nice            = 10
    disable         = no
}
EOF
    }
    elsif ("$daemon/$service" eq 'inetd/gsigatekeeper') {
        return "gsigatekeeper   stream tcp nowait root " .
               "/usr/bin/env env LD_LIBRARY_PATH=$Config{GLOBUS_LOCATION}/lib" .
               "$Config{GLOBUS_LOCATION}/sbin/globus-gatekeeper " .
               "globus-gatekeeper " .
               "-conf $Config{GLOBUS_LOCATION}/etc/globus-gatekeeper.conf\n";
    }
    elsif ("$daemon/$service" eq 'inetd/gsiftp') {
        return "gsiftp  stream  tcp     nowait  root    " .
               "/usr/bin/env env LD_LIBRARY_PATH=$Config{GLOBUS_LOCATION}/lib" .
               "$Config{GLOBUS_LOCATION}/sbin/in.ftpd " .
               "-l -a -G $Config{GLOBUS_LOCATION}\n";
    }
    else {
        die "Internal error, bad arguments to Inet_Config_Text()\n" .
            "    daemon=\"$daemon\", service=\"$service\"";
    }
} # Inet_Config_Text

# ----------------------------------------------------------------------

sub Image($) {
    my($arg) = @_;
    if (defined $arg) {
        $arg =~ s/\n/\\n/g;
        return "\"$arg\"";
    }
    else {
        return 'undef';
    }
} # Image

# ----------------------------------------------------------------------

sub Debug(@) {
    print @_ if $Debugging;
} # Debug

# ----------------------------------------------------------------------

sub Compare_Version($$) {
    my @a = split /\./, $_[0];
    my @b = split /\./, $_[1];
    my $last;
    if ($#a < $#b) {
        $last = $#a;
    }
    else {
        $last = $#b;
    }
    for (my $i = 0; $i <= $last; $i ++) {
        my $cmp = ($a[$i] <=> $b[$i]);
        if ($cmp != 0) {
            return $cmp;
        }
    }

    return 0;
} # Compare_Version

# ----------------------------------------------------------------------

sub Release_String() {
    my $at_least;
    foreach my $ref (@$Release_Info) {
        if ($ref->{revision} eq $Revision) {
            return "release $ref->{release}";
        }
        elsif (Compare_Version($ref->{revision}, $Revision) < 0) {
            $at_least = $ref->{release};
        }
    }
    if (defined $at_least) {
        return "release $at_least+";
    }
    else {
        return "Unknown release";
    }
} # Release_String

# ----------------------------------------------------------------------

sub Show_Version() {
    print "$Prog_Name ", Release_String(), " (revision $Revision)\n";
    exit 1;
} # Show_Version

# ----------------------------------------------------------------------

sub Usage(@) {
    print @_ if @_;
    print <<"EOF";
Usage: $Prog_Name [options] config_file
$Prog_Name uses Perl's Getopt::Long module to parse options.
Options may be prefixed with either "-" or "--".
Option names may be abbreviated uniquely.
    -help               Display this message and exit.
    -version            Display version information and exit.
    -validate-only      Validate the configuration file and exit.
    -cleanup            Delete temporary directory on completion (default).
    -postinstall        Run gpt-postinstall (default).
    -startup            Update system files and start services
                        (default is -nostartup)
    -gpt-verbose        Pass -verbose option to gpt-install and/or gpt-build.
    -gpt-install-only   Pass -install-only option to gpt-build.
                        Doesn't affect binary installations (gpt-install).
    -gpt-ignore-errors  Ignore errors from gpt-install and gpt-build.
    -aix-kludge         Use an existing GPT installation.
    -debugging          Enable debugging.
Most options can be negated by prepending "no".
For example, "-nocleanup" inhibits cleanup.
EOF
    exit 1;
#
# Disabled option:
#   -gpt-log            Pass -log=... option to gpt-build.
#                       Implies -nocleanup, since logs are written to the
#                       temporary directory.
#                       Doesn't affect binary installations (gpt-install).
#
} # Usage
